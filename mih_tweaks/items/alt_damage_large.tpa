DEFINE_ACTION_FUNCTION alt_damage_large
BEGIN
  COPY + "%MOD_FOLDER%/%component_loc%/tables/_alt_dam.2da" "override"

  COPY_EXISTING_REGEXP "^.*\.itm$" "override"
    LPF struct_read RET_ARRAY m = struct END
    SET found = 0

    INNER_PATCH_FILE "_alt_dam.2da"
    BEGIN
      COUNT_2DA_COLS cols
      COUNT_2DA_ROWS cols rows

      FOR (SET i = 0; i < rows; ++i)
      BEGIN
        READ_2DA_ENTRY i 1 cols match_cat
        READ_2DA_ENTRY i 2 cols match_prof

        PATCH_IF m_category = match_cat AND (match_prof = 0
       	      OR IDS_OF_SYMBOL("stats" "%m_proficiency%") = match_prof)
	BEGIN
	  SET found = 1

          READ_2DA_ENTRY i 3 cols alt_dicenum
          READ_2DA_ENTRY i 4 cols alt_dicesize
          READ_2DA_ENTRY i 5 cols alt_bonus
	END
      END
    END

    PATCH_IF found
    BEGIN
      m.ab.alter{s_alternate_dicenumber = alt_dicenum;;s_alternate_dicesize = alt_dicesize;;s_alternate_damagebonus = alt_bonus + m_enchantment|match=~"%s_type%" == "melee"~}
      LPF struct_write STR_VAR struct = "m" END
    END

    BUT_ONLY_IF_IT_CHANGES

  DELETE + "override/_alt_dam.2da"


  COPY - "%MOD_FOLDER%/%component_loc%/tables/_alt_item.2da" "%workspace%"
    COUNT_2DA_COLS cols
    COUNT_2DA_ROWS cols rows

    FOR (SET i = 0; i < rows; ++i)
    BEGIN
      READ_2DA_ENTRY i 0 cols item
      READ_2DA_ENTRY i 1 cols alt_dicenum
      READ_2DA_ENTRY i 2 cols alt_dicesize
      READ_2DA_ENTRY i 3 cols alt_bonus

      PATCH_IF FILE_EXISTS_IN_GAME "%item%.itm"
      BEGIN
        INNER_ACTION
        BEGIN
	  itm.edit["%item%"]
	  [
            m.ab.alter{s_alternate_dicenumber = alt_dicenum;;s_alternate_dicesize = alt_dicesize;;s_alternate_damagebonus = alt_bonus + m_enchantment|match=~"%s_type%" == "melee"~}
	  ]
        END
      END
    END


  COPY_EXISTING_REGEXP "^.+\.cre$" "override"
    LPF struct_read RET_ARRAY m = struct END
    
    PATCH_IF INDEX(CASE_INSENSITIVE EXACT_MATCH "%m_race%" "ankheg basilisk bear carrioncrawler gnoll ogre spider wyvern demonic beholder otyugh troll umberhulk giant golem elemental dragon minotaur drider shambling_mound chimera yeti treant ettin") != "-1"
    BEGIN
      m_bit_13 = 1
      LPF struct_write STR_VAR struct = "m" END
    END

    BUT_ONLY_IF_IT_CHANGES
END	// alt_damage_large


