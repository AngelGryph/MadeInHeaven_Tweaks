DEFINE_ACTION_FUNCTION adjust_xp_rewards
BEGIN
  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "xp_percentage_monsters"
    RET
    xp_percentage_monsters	= value
  END

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "xp_percentage_group"
    RET
    xp_percentage_group		= value
  END

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "xp_percentage_individual"
    RET
    xp_percentage_individual	= value
  END


  ACTION_IF xp_percentage_monsters != 100
  BEGIN
    LAUNCH_ACTION_FUNCTION edit_all_creatures
      STR_VAR
      editstring	= ~xp_value=>"(xp_value * %xp_percentage_monsters%) / 100"~
    END
  END

  ACTION_IF xp_percentage_individual != 100
         OR xp_percentage_group != 100
  BEGIN
    COPY_EXISTING_REGEXP "^.*\.bcs$" "override"
                         "^.*\.dlg$" "override"
      DECOMPILE_AND_PATCH
      BEGIN
        PATCH_IF xp_percentage_group != 100
	BEGIN
          REPLACE_EVALUATE CASE_INSENSITIVE
            ~AddExperienceParty(\([0-9]+\))~
	    BEGIN
	      SET xp = (MATCH1 * xp_percentage_group) / 100
	    END
	    ~AddExperienceParty(%xp%)~
	END

        PATCH_IF xp_percentage_individual != 100
	BEGIN
          REPLACE_EVALUATE CASE_INSENSITIVE
            ~AddXPObject(\(.+\), *\([0-9]+\))~
	    BEGIN
	      SET xp = (MATCH2 * xp_percentage_individual) / 100
	    END
	    ~AddXPObject(%MATCH1%, %xp%)~
        END
      END

    BUT_ONLY_IF_IT_CHANGES


    COPY_EXISTING "xplist.2da" "override"
      COUNT_2DA_COLS cols
      COUNT_2DA_ROWS cols rows

      READ_2DA_ENTRIES_NOW __xplist_read cols

      FOR (SET i = 0; i < rows; ++i)
      BEGIN
        READ_2DA_ENTRY_FORMER __xplist_read i 2 old_xp

        INNER_PATCH_SAVE new_xp ~%old_xp%~
        BEGIN
          REPLACE_EVALUATE CASE_INSENSITIVE
            ~^\([0-9]+\)$~
	    BEGIN
	      SET xp = (MATCH1 * xp_percentage_individual) / 100
	    END
	    ~%xp%~

          REPLACE_EVALUATE CASE_INSENSITIVE
            ~^P_\([0-9]+\)$~
	    BEGIN
	      SET xp = (MATCH1 * xp_percentage_group) / 100
	    END
	  ~P_%xp%~
        END

        SET_2DA_ENTRY_LATER __xplist_set i 2 ~%new_xp%~
      END

      SET_2DA_ENTRIES_NOW __xplist_set cols
      PRETTY_PRINT_2DA

      BUT_ONLY_IF_IT_CHANGES
  END


  ACTION_IF xp_percentage_individual != 100
  BEGIN
    COPY_EXISTING "xpbonus.2da" "override"
      COUNT_2DA_COLS cols
      COUNT_2DA_ROWS cols rows

      READ_2DA_ENTRIES_NOW __xpbonus_read cols

      FOR (SET i = 0; i < rows; ++i)
      BEGIN
        FOR (SET j = 1; j < cols; ++j)
        BEGIN
          READ_2DA_ENTRY_FORMER __xpbonus_read i j old_xp

	  SET new_xp = (old_xp * xp_percentage_individual) / 100

          SET_2DA_ENTRY_LATER __xpbonus_set i j ~%new_xp%~
        END
      END

      SET_2DA_ENTRIES_NOW __xpbonus_set cols
      PRETTY_PRINT_2DA

      BUT_ONLY_IF_IT_CHANGES
  END
END	// adjust_xp_rewards


