DEFINE_ACTION_FUNCTION monster_bonus_hp
BEGIN
  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "bonus_hp_per_hd_animal"
    RET
    bonus_hp_per_hd_animal	= value
  END

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "bonus_hp_per_hd_undead"
    RET
    bonus_hp_per_hd_undead	= value
  END

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "bonus_hp_per_hd_gianthumanoid"
    RET
    bonus_hp_per_hd_gianthumanoid	= value
  END

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "bonus_hp_per_hd_monster"
    RET
    bonus_hp_per_hd_monster	= value
  END

  COPY_EXISTING_REGEXP ".+\.cre$" "override"
    LAUNCH_PATCH_FUNCTION CRE_read_general
      RET
      general	= value
    END

    TO_LOWER general

    PATCH_IF VARIABLE_IS_SET "bonus_hp_per_hd_%general%"
    BEGIN
      SET bonus_hp_per_hd = EVAL "%bonus_hp_per_hd_%general%%"
    END
    ELSE
    BEGIN
      SET bonus_hp_per_hd = 0
    END

    PATCH_IF bonus_hp_per_hd > 0
    BEGIN
      PATCH_MAKE_PATCH
        var_bonus_hp=>"level1 * %bonus_hp_per_hd%"
	hp_current=>"hp_current + bonus_hp"
	hp_max=>"hp_max + bonus_hp"
      END

      LAUNCH_PATCH_FUNCTION apply_patches
        STR_VAR
	file_ext	= "CRE"
	edits		= "patch_data"
      END
    END

    BUT_ONLY_IF_IT_CHANGES
END	// monster_bonus_hp


